plugins {
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '2.1.21'
    id 'org.jmailen.kotlinter' version '5.2.0'
    id 'com.gradleup.shadow' version '8.3.1'
}

group 'no.nav.sf.brukernotifikasjon'
version '0.1'

jar {
    manifest {
        attributes 'Main-Class': 'no.nav.sf.brukernotifikasjon.BootstrapKt'
    }
}

shadowJar {
    archiveBaseName = 'app'
    archiveClassifier = ''
}

application {
    mainClass = 'no.nav.sf.brukernotifikasjon.BootstrapKt'
}

run {
}

repositories {
    mavenCentral()

    maven {
        url "https://packages.confluent.io/maven"
    }

    maven {
        url "https://jitpack.io"
    }

    ["maven-release"].each { repo ->
        String token = System.getenv("GITHUB_TOKEN") ?: project.findProperty("githubPassword")
        if (!token) throw new NullPointerException("Missing token, you have to set GITHUB_TOKEN or githubPassword")
        maven {
            url = uri("https://maven.pkg.github.com/navikt/$repo")
            credentials {
                username = 'x-access-token'
                password = token
            }
        }
    }
}

configurations {
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')

    implementation "no.nav.security:token-validation-core:5.0.29"

    implementation 'com.google.code.gson:gson:2.13.2'

    implementation "com.github.navikt:brukernotifikasjon-schemas:2.6.0"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.9.0"

    implementation "org.http4k:http4k-core:6.20.1.0"
    implementation "org.http4k:http4k-server-netty:6.20.1.0"
    implementation "org.http4k:http4k-client-okhttp:6.20.1.0"

    implementation "org.apache.kafka:kafka-clients:7.9.5-ce"
    implementation "io.confluent:kafka-avro-serializer:7.9.5"

    implementation "io.github.microutils:kotlin-logging:3.0.5"
    implementation "ch.qos.logback:logback-classic:1.5.20"
    implementation "net.logstash.logback:logstash-logback-encoder:8.1"

    implementation "io.prometheus:simpleclient_common:0.8.1"
    implementation "io.prometheus:simpleclient_hotspot:0.8.1"

    implementation "org.apache.commons:commons-lang3:3.18.0"
    implementation "org.xerial.snappy:snappy-java:1.1.10.4" //Desired transient snappy to kafka-clients above
    implementation "org.apache.avro:avro:1.11.5" //Desired transient avro to serializer above
    implementation "no.nav.tms.varsel:kotlin-builder:2.1.1"
    implementation "at.yawk.lz4:lz4-java:1.10.1"

    testImplementation "org.junit.jupiter:junit-jupiter:5.11.4"
    testImplementation 'io.mockk:mockk:1.14.2'
}

// Transitive dependencvy lz4-java is fixed only in fork at.yawk, therefore this reference:
configurations.all {
    resolutionStrategy {
        capabilitiesResolution.withCapability("org.lz4:lz4-java") { details ->
            // Prefer the patched fork
            details.select("at.yawk.lz4:lz4-java:1.10.1")
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    test.kotlin.srcDirs += 'src/test/kotlin'
}

kotlin {
    jvmToolchain(21)
}
