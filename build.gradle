buildscript {
    ext.kotlin_version = '1.3.61'
    ext.kotlin_coroutine = '1.3.3'
    ext.http4k_version = '3.242.0'
    ext.kafka_version = '2.3.1'
    ext.confluent_version = '5.4.0'
    ext.embedded_kafka = '2.4.0'
    ext.kotlin_loggin_version = '1.7.8'
    ext.logback_version = '1.2.3'
    ext.log4j_version = '1.7.25'
    ext.logstash_encoder_version = '6.3'
    ext.prometheus_version = '0.8.0'
    ext.kotest_version = '4.0.2'
    ext.kotlin_serialization = '0.14.0'
    ext.crm_utilities_version = '0.109'
    //ext.brukernotifikasjonerVersion = '1.2021.08.27-09.12-f8a8cbc76319'
    ext.brukernotifikasjonerVersion = 'v1.1.0'
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.61'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.3.61'

    id 'application'
    id 'idea'

    id 'org.jmailen.kotlinter' version '2.2.0'
    id 'com.github.ben-manes.versions' version '0.27.0'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

group 'no.nav.sf.brukernotifikasjon'
version '0.1'

jar {
    manifest {
        attributes 'Main-Class': 'no.nav.sf.brukernotifikasjon.MainKt'
    }
}

shadowJar {
    archiveBaseName = 'app'
    archiveClassifier = ''
}

application {
    mainClassName = 'no.nav.sf.brukernotifikasjon.MainKt'
}

tasks.withType(JavaExec) {
    jvmArgs = ['-Xms1024m', '-Xmx2048m']
}

run {
    environment "KAFKA_BROKERS", "localhost:9092"
    environment "KAFKA_CLIENTID", "sf-brukernotifikasjon"
    environment "KAFKA_SECURITY", "FALSE"
    environment "KAFKA_TOPIC", "aapen-oppgave-endret-v1-dev-syntetisk"
    environment "SF_INSTTYPE", "SCRATCH"
    environment "MS_BETWEEN_WORK", "120000"
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "http://packages.confluent.io/maven"
    }
    maven {
        url "https://packages.confluent.io/maven/"
    }
    maven {
        url "https://jitpack.io"
    }

    ["maven-release", "crm-utilities"].each { repo ->
        String token = System.getenv("GITHUB_TOKEN") ?: project.findProperty("githubPassword")
        if (!token) throw new NullPointerException("Missing token, you have to set GITHUB_TOKEN or githubPassword")
        maven {
            url = uri("https://maven.pkg.github.com/navikt/$repo")
            credentials {
                username = 'x-access-token'
                password = token
            }
        }
    }
}

configurations {
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
}

dependencies {
    implementation platform('org.jetbrains.kotlin:kotlin-bom')

    implementation 'com.google.code.gson:gson:2.8.6'

    compile "no.nav.sf.library:crm-utilities:$crm_utilities_version"

    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.12.1"

    implementation "com.github.navikt:brukernotifikasjon-schemas:$brukernotifikasjonerVersion"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutine"
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$kotlin_serialization"

    implementation "org.http4k:http4k-core:$http4k_version"
    implementation "org.http4k:http4k-server-netty:$http4k_version"
    implementation "org.http4k:http4k-client-apache:$http4k_version"
    implementation "org.http4k:http4k-format-jackson:$http4k_version"

    implementation "org.apache.kafka:kafka-clients:$kafka_version"
    implementation "io.confluent:kafka-avro-serializer:$confluent_version"

    implementation "io.github.microutils:kotlin-logging:$kotlin_loggin_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"
    implementation "net.logstash.logback:logstash-logback-encoder:$logstash_encoder_version"

    implementation "io.prometheus:simpleclient_common:$prometheus_version"
    implementation "io.prometheus:simpleclient_hotspot:$prometheus_version"

    testImplementation "io.kotest:kotest-runner-junit5-jvm:$kotest_version"
    testImplementation "io.kotest:kotest-assertions-core-jvm:$kotest_version"
    testImplementation "no.nav:kafka-embedded-env:$embedded_kafka"
    testImplementation "org.http4k:http4k-server-ktorcio:$http4k_version"
}

test {
    useJUnitPlatform()
}

sourceSets {
    main.kotlin.srcDirs += 'src/main/kotlin'
    test.kotlin.srcDirs += 'src/test/kotlin'
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}